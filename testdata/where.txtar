# missing files should be reported and fail
! exec parquetry where 'a eq 3' missing.parquet
stderr 'missing.parquet: no such file or directory'
! stdout .

# bad filters should be reported and fail
! exec parquetry where 'a eq a' missing.parquet
stderr .
! stdout .

# alphav: only matching rows
exec parquetry where 'A eq ''d'' OR A eq ''f''' alphav.parquet
cmp stdout alphav.want

# alphaw: include matching row
exec parquetry where 'A eq ''f'' OR A eq ''a''' alphaw.parquet
cmp stdout alphaw.want

# alphamapv: only matching rows
exec parquetry where 'M.B ne ''b''' alphamapv.parquet
cmp stdout alphamapv.want

# alphamapw: matching row
exec parquetry where 'M.B ne ''C''' alphamapw.parquet
cmp stdout alphamapw.want

# times should reflect precision. Sus/Sns aren't marked as times.
exec parquetry where 'Date gt ''2011-11-11''' times.parquet
cmp stdout times.want
exec parquetry where 'Date lt ''2011-11-11''' times.parquet
! stdout .

-- alphav.want --
{A:d}
{A:f}
-- alphaw.want --
{A:a B:b C:c D:d E:e F:f G:g}
-- alphamapv.want --
{M:map[A:a]}
{M:map[C:c]}
-- alphamapw.want --
{M:map[A:a B:b C:c]}
-- times.want --
{Date:2024-12-18 Stamp:2024-12-18T09:23:19.123Z Sms:2024-12-18T09:23:19.123Z Sus:1734513799123456 Sns:1734513799123456789 Time:2024-12-18T09:23:19.123Z Tms:2024-12-18T09:23:19.123Z Tus:2024-12-18T09:23:19.123456789Z Tns:2024-12-18T09:23:19.123456789Z}
